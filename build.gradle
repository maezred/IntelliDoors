buildscript {
    ext.kotlin_version = '1.1.1'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath 'org.ajoberstar:gradle-git:1.7.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'org.ajoberstar.release-opinion'
apply plugin: 'com.github.johnrengelman.shadow'

ext {
    artifactId = project.name.toLowerCase()
    author = grgit.log(maxCommits: 1).get(0).author
    buildDateTime = new Date().format('YYYY/MM/dd HH:mm:ss')
    buildDate = new Date().format('YYYY/MM/dd')
    buildTime = new Date().format('HH:mm:ss')
    isDirty = "${project.version}".contains('uncommitted')
    isSnapshot = "${project.version}".contains('dev')
    website = "https://github.com/moltendorf/${project.name}"
}

description = 'Smarter doors for your Minecraft world!'
group = 'net.moltendorf.bukkit'

sourceCompatibility = 1.8

task expandTemplates(type: Copy) {
    outputs.upToDateWhen { false }

    from "$projectDir/src/main/templates"
    into "$buildDir/resources/main"

    expand project: project
}

task copySources(type: Copy) {
    outputs.upToDateWhen { false }

    from "$projectDir/src"
    into "$buildDir/resources/main/src"
}

task deleteSources(type: Copy) {
    outputs.upToDateWhen { false }

    delete "$buildDir/resources/main/src"
}

tasks.build.mustRunAfter 'clean'
tasks.expandTemplates.mustRunAfter 'copySources'
tasks.processResources.mustRunAfter 'copySources'
tasks.shadowJar.mustRunAfter 'jar'

tasks.assemble.dependsOn 'shadowJar'
tasks.copySources.dependsOn 'deleteSources'
tasks.processResources.dependsOn 'expandTemplates'
tasks.publish.dependsOn 'clean', 'build'
tasks.release.dependsOn 'publish'

if (project.ext.isDirty) {
    tasks.publish.dependsOn 'copySources'
} else {
    tasks.publish.dependsOn 'clean'
}

jar {
    manifest.attributes 'Specification-Title': project.name,
            'Specification-Vendor': "${project.ext.author.name} <${project.ext.author.email}>",
            'Implementation-Title': "${project.group}.${project.ext.artifactId}.${project.name}",
            'Implementation-Version': project.version
}

shadowJar {
    baseName = project.name
    version = project.version
    classifier = null
}

publishing {
    publications {
        shadow(MavenPublication) {
            groupId = project.group
            artifactId = project.ext.artifactId

            from components.shadow
        }
    }

    repositories {
        maven {
            if (project.ext.isSnapshot) {
                url "file://$projectDir/out/nexus/snapshots"
            } else {
                url "file://$projectDir/out/nexus/releases"
            }
        }
    }
}

repositories {
    mavenCentral()

    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public' }
    maven { url 'https://fab.moltendorf.net/nexus/releases' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlin_version}"

    compile 'org.spigotmc:spigot-api:1.9+'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

configurations {
    runtime.exclude module: 'spigot-api'
}
